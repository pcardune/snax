# Language Overview

The following is a quick overview of the snax programming language.

## Hello World

Because you have to get hello world out of the way really quickly, here it is:

```snax
import io from "snax/io.snx"

io::printStr(@"Hello, world!");
```

## Functions

Use the `func` keyword to define a function. Parameters must be typed,
but the return type is optional, and will be inferred from the return statement
if it's missing.

```snax
func add(x:i32, y:i32) {
  return x+y;
}
add(1, 2);
```

## Global Variables

Global variables are declared with the `global` keyword and
can be accessed from inside functions.

```snax
global counter = 0;
func count() {
  counter = counter + 1;
}
count();
count();
counter;
```

## Local Variables

Local variables are declared with the `let` keyword and
can only be accessed from inside the block they are declared in.

Note that the `{...}` syntax creates a new lexical scope:

```snax
let x = 1;
{
  let x = 2;
}
x;
```

## Register Variables

Register variables are declared with the `reg` keyword and
can only store numeric types. They are allocated on the
web assembly execution stack, and can be faster than local variables.
They do not have memory addresses and can not be passed by reference.

```snax
reg x = 3;
x;
```

## Control Flow

Snax supports `while` loops:

```snax
let i = 0;
let s = 1;
while (i < 10) {
  let j = 0;
  while (j < i) {
    j = j+1;
    s = s + j;
  }
  i = i+1;
}
s;
```

And also `if` statements:

```snax
let x=3;
if (x==4) {
  x=7;
} else {
  x=9;
}
x;
```

## Arrays

You can create an array with `[...]` brackets.

```snax
let arr = [1,2,3];
arr[2];
```

## Structs

You can define new compound types with the `struct` keyword.

```snax
struct Vector {
  x: i32;
  y: i32;
}

let v = Vector::{ x: 3, y: 5 };
v.x;
```

Tuple structs are structs with unnamed fields:

```snax
struct Vector(u8, i32);
let v:Vector;
v.0 = 23_u8;
v.1 = 1234;
v.0;
```

## Pointers and References

Pointers can be used for passings things around by reference.
You use the `@` operator to the the @ddress of a variable (see what I did there?), and you use the `&` operator to specify that a type is a reference.
When you see `@x` in the code, it means "the address of x", and when you see `&i32` it means "a reference to an i32 value".

Dereferencing a pointer is done with array indexing. So if you have an
`let x:&i32` variable, you can get/set the value at that location with `x[0]`.

```snax
func increment(x: &i32) {
  x[0] = x[0] + 1;
}

let a:i32 = 5;

increment(@a);
increment(@a);
increment(@a);

a;
```

## Modules and Standard Library

Modules can be imported with the `import` keyword. All standard library
modules are prefixed with `snax/`. You can use functions from the
module by prefixing them with the module name and `::`.

```snax
import math from "snax/math.snx"

math::sinf32(2.0);
```

You can also define your own modules, using the `module` keyword. Each
module has it's own namespace. Each file is implicitly a module, as well.

```snax
import stdmath from "snax/math.snx"

module mymath {
  func add(a:f32, b:f32) {
    return a+b;
  }
}

mymath::add(1.0, stdmath::cosf32(2.0));
```

## Compiler Functions

The snax compiler exposes certain additional functionality which is tied to
the compiler implementation. It's generally not a good idea to use these
functions directly, but you will see them referenced in the standard library.
These functions are accessed using the `$` prefix. For example, you can get
access to the heap pointer with `$heap_start()`.

```snax
let a = "Static strings take up space in memory.";
let b = "As a result, the heap will begin at a later point.";
$heap_start();
```

As another example, the `$size_of()` function returns the number of
bytes used for a particular value.

```snax
struct Vector {
  x: f64;
  y: f64;
}
let v = Vector::{x: 1.34, y:3.45};
// each f64 in the struct takes up 8 bytes,
// so the size of the struct is 16 bytes
$size_of(v);
```

{/*
## Getting Started

To get started, you'll need to install the snax compiler:

```bash
npm install -g snax-lang
```

Then, create a file called `hello.snx` with the following contents:

```snx
import io from "snax/io.snx";

io::print(@"Hello, world!");
```

Finally, compile and run your program:

```bash
snax hello.snx
```

## Documentation

- [Language Reference](/snax-book/snax/language-reference)
- [Standard Library](/snax-book/snax/standard-library)
- [Snax Book](/snax-book)

## TODO: Language Reference Outline

- [ ] Introduction
  - [ ] Hello, world!
  - [ ] Comments
  - [ ] Variables
  - [ ] Functions
  - [ ] Control Flow
  - [ ] Structs
  - [ ] Arrays
  - [ ] Pointers
  - [ ] Memory Management
  - [ ] Modules
  - [ ] Standard Library
    - [ ] IO
    - [ ] Math
    - [ ] Strings
    - [ ] Memory Allocator
- [ ] Advanced Topics
  - [ ] Running in a web browser
  - [ ] Interfacing with JavaScript
- [ ] FAQ
  - [ ] Why another programming language?
  - [ ] Why is it called Snax?
  - [ ] Who is the mascot?

*/}